create or replace procedure p_cncp_sale
/****************************************************************/
   -- 渠道商售票
   -- add by Chen Zhen @ 2016-09-09
   -- BUG 57 modify by kwx @ 2016-12-05
/*************************************************************/
(
  --------------输入----------------
  p_flow                              in char,          -- 申请流水
  p_game                              in number,        -- 游戏编码
  p_issue                             in number,        -- 游戏期号
  p_dealer                            in char,          -- 渠道编码
  p_amount                            in number,        -- 调整金额
  p_detail                            in varchar2,      -- 投注详情（JSON格式）
--  p_key                               in varchar2,      -- 秘钥

  --------------输入----------------
  c_error_code                        out number,        -- 错误码
  c_error_msg                         out varchar2       -- 错误内容

 ) is

  v_be_balance               number(28);                 -- 修改前账户余额
  v_af_balance               number(28);                 -- 修改后账户余额
  v_be_frozen_balance        number(28);                 -- 修改前账户冻结余额
  v_af_frozen_balance        number(28);                 -- 修改后账户冻结余额
  
  v_input_json               json;
  temp_json_list          json_list;

begin

  dbtool.set_success(errcode => c_error_code, errmesg => c_error_msg);

  -- 限制申请流水长度必须为24
  if length(trim(p_flow)) <> 24 then
    c_error_code := 1;
    c_error_msg := ecncp_error.dealer_status_invalid;
    return;
  end if;
  
  -- 检查渠道状态
  if not f_cncp_check_dealer_status(p_dealer) then
    c_error_code := 1;
    c_error_msg := ecncp_error.dealer_status_invalid;
    return;
  end if;
  
  -- 检查渠道游戏授权
  if not f_cncp_check_dealer_game(p_dealer, p_game) then
    c_error_code := 2;
    c_error_msg := ecncp_error.dealer_game_not_auth;
    return;
  end if;
  
  -- 检查游戏期次状态，应该是在售状态
  if not f_check_game_issue_sale(p_game, p_issue) then
    c_error_code := 3;
    c_error_msg := ecncp_error.dealer_issue_not_open;
    return;
  end if;
  
  -- 插入记录
  insert into cncp_trade_sale
    (sale_flow, game_code, issue_number, dealer_code, order_status,                       order_amount, order_time, sell_seq)
  values
    (p_flow,    p_game,    p_issue,      p_dealer,    ecncp_dealer_order_status.accepted, p_amount,     sysdate,    f_get_cncp_sale_seq);

  -- 插入投注明细
  v_input_json := json(p_detail);
  temp_json_list := json_list(v_input_json.get('betLines'));

  for v_loop_i in 1..temp_json_list.count loop
    insert into cncp_trade_sale_detail
      (sale_flow, line_no,  ticket_detail)
    values
      (p_flow,    v_loop_i, temp_json_list.get(v_loop_i).get_string);
  end loop;
  
  -- 扣钱
  p_cncp_dealer_fund_change(p_dealer, ecncp_flow_type.sale, p_amount, 0, p_flow, v_be_balance, v_af_balance, v_be_frozen_balance, v_af_frozen_balance);

  COMMIT;

EXCEPTION
  WHEN OTHERS THEN
    ROLLBACK;
    c_error_code := -1;
    c_error_msg := error_msg.ERR_COMMON_1 || SQLERRM;

end;
